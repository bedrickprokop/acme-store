apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.acmestore"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        debug {
            // Run code coverage reports by default on debug builds.
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        mock {
            dimension "version"
            applicationIdSuffix = ".mock"
            versionNameSuffix "-mock"
        }
        prod {
            dimension "version"
        }
    }

    // Remove mockRelease buildType as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release' && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true);
        }
    }

    sourceSets {
        main {
            res.srcDirs = ['src/main/res', 'src/main/res_styles', 'src/main/res_themes']
        }
        mock { java.srcDirs = ['src/mock/java'] }
        prod { java.srcDirs = ['src/prod/java'] }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

dependencies {

    // LIBS
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // ANDROIDX
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation 'androidx.core:core-ktx:1.6.0'

    // KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin"

    // RETROFIT
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"

    // RETROFIT
    mockImplementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    mockImplementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"

    // TESTS
    testImplementation "junit:junit:$versions.junit"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espresso"

}